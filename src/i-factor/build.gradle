import org.apache.tools.ant.filters.ReplaceTokens

project.ext {
  baseProject = project(':i-factor.mgt')
  basePackage = 'com.entrofine'
  baseName = name
  
  version = '1.0-SNAPSHOT'
  activitiVersion = '5.15.1'
  bcprovVersion = '1.46'
  commonsCodecVersion = '1.9'
  commonsFileuploadVersion = '1.3.1'
  findbugsVersion = '2.0.1'
  groovyVersion = '2.3.0'
  guavaVersion = '17.0'
  freemarkerVersion = '2.3.20'
  hibernateVersion = '4.3.5.Final'
  hibernateValidatorVersion = '5.1.2.Final'
  jacksonVersion = '2.3.3'
  javaxMailVersion = '1.4.7'
  jstlVersion = '1.2'
  junitVersion = '4.11'
  logbackVersion = '1.1.2'
  servletVersion = '2.5'
  shiroVersion = '1.2.3'
  slf4jVersion = '1.6.2'
	springDataJpaVersion = '1.5.2.RELEASE'
  springVersion = '3.2.8.RELEASE'
}

subprojects {
  apply plugin: 'java'
  apply plugin: 'eclipse'
  apply plugin: 'sonar-runner'
  apply plugin: 'war'
  
  sonarRunner {
    sonarProperties {
      property "sonar.sourceEncoding", "UTF-8"
      property "sonar.host.url", "http://localhost:9000"
      property "sonar.jdbc.url", "jdbc:h2:tcp://localhost:9092/sonar"
      property "sonar.jdbc.driverClassName", "org.h2.Driver"
      property "sonar.username", "sonar"
      property "sonar.password", "sonar"
    }
  }

  processResources {
    filter ReplaceTokens, tokens: [
      'package.base': basePackage, 'project.name': project.name
    ]
  }

  configurations {
    jdbc
    h2
    mysql
    oracle
    db2
    sql_server
  }

  project.ext {
    application = [
      properties: new Properties(),
      test: [ properties: new Properties() ]
    ]
    
    def appConf = file("src/main/resources/application.properties")
    if ( appConf.exists() ) {
      appConf.withInputStream { stream -> application.properties.load(stream) }
    }

    application.test.properties << application.properties
    appConf = file("src/test/resources/application.test.properties")
    if ( appConf.exists() ) {
      appConf.withInputStream { stream -> application.test.properties.load(stream) }
    }
  }

  eclipse {
    classpath {
      defaultOutputDir = file("build/eclipse")
    }
  }

  sourceSets {
    main {
      java {
        srcDir 'src/main/java'
      }
      resources {
        srcDir 'src/main/resources'
      }
    }
  }

  [compileJava, compileTestJava]*.options.collect { options -> 
    options.encoding = 'UTF-8'
  }

  dependencies {
    def lib = new File(project(':').projectDir.parentFile, 'lib/flat')
    
    h2 "com.h2database:h2:1.3.171"
    mysql "mysql:mysql-connector-java:5.1.24"
    oracle files(new File(lib, 'oracle/ojdbc6.jar'))
    db2 files(new File(lib, 'db2/db2jcc.jar'))
    sql_server files(new File(lib, 'sql_server/sqljdbc4.jar'))

    def database = project.application.properties['jdbc.database']
    if (database) {
      jdbc configurations[database.toLowerCase()]
    }

    database = project.application.test.properties['jdbc.database']
    if (database) {
      testRuntime configurations[database.toLowerCase()]
    }
  }

  repositories {
    maven {
      url(new File(project(':').projectDir.parentFile, 'lib/repository').toURI().toURL())
    }
    maven {
      url "http://localhost:8081/nexus/content/groups/public/"
    }
    mavenCentral name:'codehaus', artifactUrls: ["http://repository.codehaus.org/"]
    mavenCentral name:'ibiblio', artifactUrls: ["http://mirrors.ibiblio.org/pub/mirrors/maven2/"]
    mavenCentral name:'apache', artifactUrls: ["http://people.apache.org/repo/m2-ibiblio-rsync-repository/"]
    mavenCentral name:'maven', artifactUrls: ["http://repo2.maven.org/maven2/"]
    mavenCentral name:'restlet', artifactUrls: ["http://maven.restlet.org/"]
    mavenCentral name:'lds', artifactUrls: ["https://code.lds.org/nexus/content/repositories/thirdparty/"]
    mavenCentral name:'m2m', artifactUrls: ["http://repository.m2m.io:8081/nexus/content/groups/public/"]
  }
}
